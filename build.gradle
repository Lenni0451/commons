plugins {
    id "net.lenni0451.template-processor" version "1.0.0" apply false
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"
    apply plugin: "checkstyle"
    apply plugin: "net.lenni0451.template-processor"

    base {
        java.toolchain.languageVersion = JavaLanguageVersion.of(8)
        compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

        archivesName = "commons-" + project.maven_name
        group = project.maven_group ?: rootProject.maven_group
        version = project.maven_version ?: rootProject.maven_version
    }

    repositories {
        mavenCentral()
        maven {
            name = "lenni0451 releases"
            url = "https://maven.lenni0451.net/releases"
        }
    }

    dependencies {
        compileOnly "com.google.code.findbugs:jsr305:3.0.2"
        compileOnly "org.jetbrains:annotations:24.1.0"
        compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.34"))

        testImplementation platform("org.junit:junit-bom:5.9.3")
        testImplementation "org.junit.jupiter:junit-jupiter"
        testCompileOnly(testAnnotationProcessor("org.projectlombok:lombok:1.18.34"))
        testImplementation "org.ow2.asm:asm:9.6"
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    javadoc {
        failOnError = false
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    templateProcessor {
        templateDir = project.file("src/main/templates")
        outputDir = layout.buildDirectory.dir("generated/templates/main/java")
        markAsSource(project.sourceSets.main)
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxParallelForks Runtime.runtime.availableProcessors()
    }

    publishing {
        repositories {
            maven {
                name = "AggregatingPublisher"
                url = "http://127.0.0.1:8080/aggregate/maven"
            }
        }
        publications {
            maven(MavenPublication) {
                artifactId = project.maven_name ?: rootProject.maven_name
                groupId = rootProject.maven_group
                version = rootProject.maven_version

                from components.java

                pom {
                    name = rootProject.name
                    description = rootProject.maven_description
                    url = "https://github.com/" + rootProject.github_repo
                    licenses {
                        license {
                            name = "MIT License"
                            url = "https://github.com/" + rootProject.github_repo + "/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "Lenni0451"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com/" + rootProject.github_repo + ".git"
                        developerConnection = "scm:git:ssh://github.com/" + rootProject.github_repo + ".git"
                        url = "github.com/" + rootProject.github_repo
                    }
                }
            }
        }
    }

    checkstyle {
        toolVersion = "9.3" //Latest version for Java 8
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    }

    signing {
        setRequired false
        sign configurations.archives
        sign publishing.publications.maven
    }

    project.tasks.withType(PublishToMavenRepository).forEach {
        it.dependsOn(project.tasks.withType(Sign))
    }
    build.dependsOn(test)
    build.dependsOn(check)
}
